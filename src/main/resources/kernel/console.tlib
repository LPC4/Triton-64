; Console subsystem
struct ColorChar {
    var ch: byte
    var fg: byte
    var bg: byte
    var reserved: byte
}

global __SCREEN_WIDTH: int = 80
global __SCREEN_HEIGHT: int = 30
global __FRAMEBUFFER_BASE: long = 0x20220000
global __console_cursor: int = 0

func __console_scroll() {
    var fb: long = __FRAMEBUFFER_BASE
    var row: int = 0

    while (row < __SCREEN_HEIGHT - 1) {
        var col: int = 0
        while (col < __SCREEN_WIDTH) {
            var src_index: int = (row + 1) * __SCREEN_WIDTH + col
            var dst_index: int = row * __SCREEN_WIDTH + col

            var src: ColorChar* = ColorChar*(fb + (src_index * 4))
            var dst: ColorChar* = ColorChar*(fb + (dst_index * 4))

            dst.ch = src.ch
            dst.fg = src.fg
            dst.bg = src.bg
            dst.reserved = src.reserved

            col = col + 1
        }
        row = row + 1
    }

    var col: int = 0
    while (col < __SCREEN_WIDTH) {
        var index: int = (__SCREEN_HEIGHT - 1) * __SCREEN_WIDTH + col
        var cell: ColorChar* = ColorChar*(fb + (index * 4))
        cell.ch = 32
        cell.fg = 0xF
        cell.bg = 0
        cell.reserved = 0
        col = col + 1
    }
}

func console_init() {
    __console_cursor = 0
    console_clear()
}

func console_clear() {
    var i: int = 0
    var total_chars: int = __SCREEN_WIDTH * __SCREEN_HEIGHT
    var fb: long = __FRAMEBUFFER_BASE

    while (i < total_chars) {
        var cell: ColorChar* = ColorChar*(fb + (i * 4))
        cell.ch = 32
        cell.fg = 0xF
        cell.bg = 0
        cell.reserved = 0
        i = i + 1
    }
    __console_cursor = 0
}

func console_backspace() {
    if (__console_cursor > 0) {
        __console_cursor = __console_cursor - 1
        var fb: long = __FRAMEBUFFER_BASE + (__console_cursor * 4)
        var cell: ColorChar* = ColorChar*(fb)
        cell.ch = 32  ; Replace with space character
    }
}

func console_putc(char: byte) {
    var fb: long = __FRAMEBUFFER_BASE

    if (char == 10) {
        var row: int = __console_cursor / __SCREEN_WIDTH
        if (row >= __SCREEN_HEIGHT - 1) {
            __console_scroll()
            __console_cursor = (__SCREEN_HEIGHT - 1) * __SCREEN_WIDTH
        } else {
            __console_cursor = (row + 1) * __SCREEN_WIDTH
        }
        return
    }

    if (__console_cursor >= __SCREEN_WIDTH * __SCREEN_HEIGHT) {
        __console_scroll()
        __console_cursor = (__SCREEN_HEIGHT - 1) * __SCREEN_WIDTH
    }

    var cell: ColorChar* = ColorChar*(fb + (__console_cursor * 4))
    cell.ch = char
    cell.fg = 0xF
    cell.bg = 0
    cell.reserved = 0

    __console_cursor = __console_cursor + 1
}

func console_puts(str: long, len: int) {
    var i: int = 0
    while (i < len) {
        console_putc(byte@(str + i))
        i = i + 1
    }
}

func print(str: long, len: int) {
    console_puts(str, len)
}

func println(str: long, len: int) {
    print(str, len)
    console_putc(10)
}

func print_raw(ptr: long, len: int) {
    console_puts(ptr, len)
}
